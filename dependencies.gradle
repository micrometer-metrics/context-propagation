def VERSIONS = [
		'org.assertj:assertj-core:latest.release',
		'org.awaitility:awaitility:latest.release',
		'org.mockito:mockito-core:latest.release',
		'org.mockito:mockito-inline:latest.release'
]

def PLATFORM_BOMS = [
		'org.junit:junit-bom:5.9.+' // Not using latest.release to avoid using a milestone version.
]

subprojects {
	plugins.withId('java-library') {
		dependencies {
			constraints {
				// Direct dependencies
				VERSIONS.each { version ->
					// java-library plugin has three root configurations, so we apply constraints too all of
					// them so they all can use the managed versions.
					api version
					compileOnly version
					runtimeOnly version
				}
			}
			PLATFORM_BOMS.each { bom ->
				api platform(bom)
				compileOnly platform(bom)
				runtimeOnly platform(bom)
			}
		}
	}
}
